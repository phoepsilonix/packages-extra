# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Bob Fanger < bfanger(at)gmail >
# Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux315-r8168
_pkgname=r8168
pkgver=8.037.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux315')
makedepends=('linux315-headers')
provides=("$_pkgname=$pkgver")
groups=('linux315-extramodules')
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2)
install=$_pkgname.install
sha256sums=('06aa1c6a58a878450798100eb5f9c3d87763c5f41e99b7d5f3b2fe78646d02b0')

_extramodules=extramodules-3.15-MANJARO
_kernver="$(cat /usr/lib/modules/$_extramodules/version || true)"

prepare(){
	cd "$_pkgname-$pkgver"

	# Fix build with Linux 3.15
	# https://code.google.com/p/r8168/issues/detail?id=19
	if grep -q PREPARE_DELAYED_WORK src/r8168_n.c; then
		sed -i 's/PREPARE_DELAYED_WORK/INIT_DELAYED_WORK/' src/r8168_n.c
	else
		error 'PREPARE_DELAYED_WORK fix for Linux 3.15 is no longer needed.'
		return 1
	fi
}

build() {
	cd "$_pkgname-$pkgver"

	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$_kernver/build \
	SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
	EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
	modules
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
