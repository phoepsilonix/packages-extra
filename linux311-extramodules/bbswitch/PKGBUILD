#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgname=linux311-bbswitch
_pkgname=bbswitch
_extramodules=extramodules-3.11-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgver=0.8
pkgrel=2
pkgdesc="kernel module allowing to switch dedicated graphics card on Optimus laptops"
arch=('i686' 'x86_64')
url=("http://github.com/Bumblebee-Project/bbswitch")
license=('GPL')
depends=('linux311')
makedepends=('linux311-headers')
provides=("$_pkgname=$pkgver")
groups=('linux311-extramodules')
install=bbswitch.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/Bumblebee-Project/bbswitch/archive/v${pkgver}.tar.gz")
sha256sums=('76cabd3f734fb4fe6ebfe3ec9814138d0d6f47d47238521ecbd6a986b60d1477')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  # KDIR is necessary even when cleaning
  #make KDIR=/usr/src/linux-${_kernver} clean
  make KDIR=/usr/src/linux-${_kernver}
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  install -D -m644 bbswitch.ko $pkgdir/usr/lib/modules/${_extramodules}/bbswitch.ko
  # gzip -9 modules
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" $startdir/*.install
}
