# Based on the file created for Arch Linux by:
# Maintainer : Thomas Baechler <thomas@archlinux.org>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

_linuxprefix=linux311
_extramodules=extramodules-3.11-MANJARO
pkgname=$_linuxprefix-nvidia-304xx
_pkgname=nvidia
pkgver=304.88
pkgrel=0.1
pkgdesc="NVIDIA drivers for linux."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=("$_linuxprefix" "nvidia-304xx-utils=${pkgver}")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver" "$_linuxprefix-nvidia-legacy")
replaces=("$_linuxprefix-nvidia-legacy")
groups=("$_linuxprefix-extramodules")
conflicts=('nvidia-96xx' 'nvidia-173xx' "$_linuxprefix-nvidia")
license=('custom')
install=nvidia.install
options=(!strip)
source=('nvidia-linux-3.10.patch'
        'nvidia-linux-3.11.patch')
sha256sums=('4546db656922c104c61411918c06b2bde4303ce9aff5bf310aa5684f514fa5c2'
            '7a16034e0c526bb40acdf0740c917803d825f7d4682a9122e8afacfe80ff46d0')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source+=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    sha256sums+=('71e10294f073e31a387da4282b95cdde5fbfc996cb1c564f290eb0df1289b7cb')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source+=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    sha256sums+=('4372cc7b82ed3b5fde41e9bf0e0c1899cfbc3e79d4ed422c499b6d41b557d2c9')
fi

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    patch -Np1 -i ${srcdir}/nvidia-linux-3.10.patch
    patch -Np2 -i ${srcdir}/nvidia-linux-3.11.patch
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
