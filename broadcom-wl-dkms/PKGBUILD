# Based on the file created for Arch Linux by: Frank Vanderham

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

# Contributor: Frank Vanderham <twelve.eighty (at) gmail.>
# Contributor: Hsin-Yi Chen (hychen) <hycehn (at) canonical.com>
# Contributor: Alberto Milone <alberto.milone (at) canonical.com>
# Contributor: Jasmine Hassan <jasmine.aura (at) gmail.com>
# Contributor: b3niup
# Upstream code branches: https://code.launchpad.net/ubuntu/+source/bcmwl
# https://bugs.launchpad.net/ubuntu/quantal/+source/bcmwl/+bug/923809 for first creation
# Also see: https://bbs.archlinux.org/viewtopic.php?id=145884

pkgname=broadcom-wl-dkms
_pkgname=broadcom-wl
pkgver=6.30.223.141
pkgrel=5
pkgdesc="Broadcom 802.11 Linux STA wireless driver BCM43142."
url="https://bbs.archlinux.org/viewtopic.php?id=145884"
arch=('any')
license=('custom')
depends=('dkms')
conflicts=('wireless-bcm43142-oneiric-dkms' 'wireless-bcm43142-dkms' 'broadcom-wl6-dkms')
source=(
	"https://launchpad.net/ubuntu/+archive/primary/+files/bcmwl_${pkgver}+bdcom.orig.tar.gz"
	"https://launchpad.net/ubuntu/+archive/primary/+files/bcmwl_${pkgver}+bdcom-0ubuntu2.diff.gz"
	'broadcom-wl-dkms.conf'
	'dkms.conf'
	'001-Arch-Makefile.patch'
	'002-Module-License.patch'
	'003-gcc.patch'
	'004-linux315.patch'
	'005-linux317.patch'
	'006-linux318.patch'
)
sha1sums=('a197b77597e2ee01303e2330c5b5622d3d624887'
          'f6b6afdf4de79c7464cac016ff07ab9d651835dd'
          'a37c5dd9ab279372f0c68595bb2a8f1fe694cd13'
          '91926bb064c00a00dbcab0d1d92c7eb976d56032'
          'e1511d9cea9584a924a7ac90609c5651464a4de7'
          '306bccd26b12c808977cacf630860e7d483d213f'
          '84ba7d65d4045e1bbdb4e8fc83b024a881e312b1'
          '0a802b21a6a5af99a4f2c91cd9cce75cd1b24b81'
          '6f2e3b8c0d6a54847c6b44620e3fb627e91219d2'
          '35476be6c83b3d1d64dac64dedf502045f37d748')

backup=('etc/modprobe.d/broadcom-wl-dkms.conf')
install=install

prepare() {
	cd "${srcdir}"

	# apply diff patch to create the debian/ folder
	patch -p1 -i bcmwl_${pkgver}+bdcom-0ubuntu2.diff

	# apply source patches
	# PATCH[0]="0001-MODULE_LICENSE.patch"
	# PATCH[1]="0002-Makefile.patch"
	# PATCH[2]="0003-Make-up-for-missing-init_MUTEX.patch"
	# PATCH[3]="0009-add-support-for-linux-3.10.0.patch"
	# PATCH[4]="0010-change-the-network-interface-name-from-eth-to-wlan.patch"
	# PATCH[5]="0011-do-not-define-__devinit-as-__init-in-linux-3.8-as-__.patch"
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0001-MODULE_LICENSE.patch
	patch -d ./bcmwl-${pkgver}+bdcom -p1 -i ../debian/patches/0002-Makefile.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0003-Make-up-for-missing-init_MUTEX.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0009-add-support-for-linux-3.10.0.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0010-change-the-network-interface-name-from-eth-to-wlan.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0011-do-not-define-__devinit-as-__init-in-linux-3.8-as-__.patch

	# patch to replace use of dkpg with uname to detect 64 vs. 32 bit
	patch -p1 -i 001-Arch-Makefile.patch

	# patch for MODULE_LICENSE
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../002-Module-License.patch

	# move folders to create Arch specific folder layout
	mv bcmwl-${pkgver}+bdcom/src/* ./

	# move Makefile to main src/ folder
	mv ./bcmwl-${pkgver}+bdcom/Makefile ./

	# fix issues with gcc 4.5.0
	patch -p1 -i 003-gcc.patch

	# fix issues with linux 3.15
	patch -p1 -i 004-linux315.patch

	# fix issues with linux 3.17
	patch -p1 -i 005-linux317.patch

	# fix issues with linux 3.18
	patch -p1 -i 006-linux318.patch

	# delete the debian folders and files not needed for packaging
	rm -R debian/
	rm -R bcmwl-${pkgver}+bdcom/
	rm bcmwl_${pkgver}+bdcom.orig.tar.gz
	rm bcmwl_${pkgver}+bdcom-0ubuntu2.diff.gz
	rm bcmwl_${pkgver}+bdcom-0ubuntu2.diff
}

package() {
	cd "${srcdir}"
	
	mkdir -p ${pkgdir}/usr/src/${_pkgname}-${pkgver}

	cp -RL * ${pkgdir}/usr/src/${_pkgname}-${pkgver}
	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 broadcom-wl-dkms.conf "${pkgdir}"/etc/modprobe.d/broadcom-wl-dkms.conf
}
