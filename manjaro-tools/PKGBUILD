# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base'
        'manjaro-tools-pkg'
        'manjaro-tools-iso'
        'manjaro-tools-yaml')
_pkgver=0.15.7
pkgver=0.15.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Development tools for Manjaro Linux'
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
source=("${pkgbase}-${_pkgver}.tar.gz::${url}/archive/${_pkgver}.tar.gz" 'manjaro-32.patch'
        "${pkgbase}-${pkgver}-git.patch::https://github.com/manjaro/manjaro-tools/compare/${_pkgver}...stable-0.15.x.patch")
sha256sums=('9c5600d82714c941cc1b705a1028bf475d37d0c37013c6cdf5c1fc0e87447bc6'
            'c943bcad1b1dfa87df94429db9c137c5d166dbd986add624f6fd22fdb984c785'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855')

prepare() {
    cd ${srcdir}/${pkgbase}-${_pkgver}
    # patches here
    sed -e "s/^Version=.*/Version=${pkgver}/" -i Makefile
    patch -p1 -i ${srcdir}/${pkgbase}-${pkgver}-git.patch
    if [ "${CARCH}" = "i686" ]; then
        patch -p2 -i ${srcdir}/manjaro-32.patch
    fi
}

build() {
    cd ${srcdir}/${pkgbase}-${_pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
    pkgdesc='Development tools for Manjaro Linux (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman-mirrorlist>=20160301')
    optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
                'manjaro-tools-iso: Manjaro Linux iso tools'
                'manjaro-tools-yaml: Manjaro Linux yaml tools')
    backup=('etc/manjaro-tools/manjaro-tools.conf')

    cd ${srcdir}/${pkgbase}-${_pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg() {
    pkgdesc='Development tools for Manjaro Linux (packaging tools)'
    depends=('namcap' 'manjaro-tools-base')
    conflicts=('devtools')

    cd ${srcdir}/${pkgbase}-${_pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-yaml() {
    pkgdesc='Development tools for Manjaro Linux (yaml tools)'
    depends=('manjaro-tools-base' 'calamares-tools' 'ruby-kwalify' 'manjaro-iso-profiles-base')

    cd ${srcdir}/${pkgbase}-${_pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_yaml
}

package_manjaro-tools-iso() {
    pkgdesc='Development tools for Manjaro Linux (ISO tools)'
    depends=('dosfstools' 'libisoburn' 'squashfs-tools' 'manjaro-tools-yaml'
            'mkinitcpio' 'mktorrent' 'grub')
    optdepends=('qemu: quickly test isos')

	cd ${srcdir}/${pkgbase}-${_pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

