# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

_pkgname=thunar
pkgname=$_pkgname-gtk3
pkgver=1.7.0
pkgrel=1
pkgdesc="Modern file manager for Xfce"
arch=('i686' 'x86_64')
url="http://thunar.xfce.org"
_commit=153f9e9f25ea9678c16714f4012c7249db63079a
license=('GPL2' 'LGPL2.1')
groups=('xfce4-gtk3')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'libgudev' 'gtk3' 'exo-gtk3' 'libxfce4util-gtk3' 'libxfce4ui-gtk3' 'libpng')
makedepends=('intltool' 'xfce4-dev-tools' 'xfce4-panel-gtk3')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'xfce4-panel-gtk3: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
conflicts=($_pkgname)
provides=($_pkgname)
#source=(http://archive.xfce.org/src/xfce/$_pkgname/${pkgver%.*}/Thunar-$pkgver.tar.bz2)
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('00048824a4c6cb1561bd0cd102e5c73bfefa679d7b158967b8d0afd84f15d319')

build() {
  cd "$_pkgname-$_commit"
  
  ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static \
        --enable-gio-unix \
        --enable-dbus \
        --enable-startup-notification \
        --enable-gudev \
        --enable-exif \
        --enable-pcre \
        --disable-debug
  make
}

package() {
  cd "$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
