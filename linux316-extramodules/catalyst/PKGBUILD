# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>


_linuxprefix=linux316
_extramodules=extramodules-3.16-MANJARO
pkgname=$_linuxprefix-catalyst
_pkgname=catalyst
pkgver=14.10.1006.1001
pkgrel=1
_tarball=14-4-rev2-linux-x86-x86-64-may6
provides=("$_pkgname=$pkgver")
groups=("$_linuxprefix-extramodules")
pkgdesc="AMD/ATI Catalyst drivers for linux. fglrx kernel module only."
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
depends=("$_linuxprefix" "catalyst-utils=${pkgver}")
makedepends=("$_linuxprefix-headers")
conflicts=('catalyst-legacy' "$_linuxprefix-catalyst-legacy")
install=catalyst.install

source=(http://www2.ati.com/drivers/linux/amd-catalyst-${_tarball}.zip
        ati_make.sh
        lano1106_fglrx_intel_iommu.patch
        lano1106_kcl_agp_13_4.patch
        makefile_compat.patch
        kolasa_fglrx_312.patch
	cold-fglrx-3.14-current_euid.patch)

sha256sums=('93e213e5b182a0863aa926d1e3ab4498adc0e0007aac4fc14a3abae84bb170f5'
            '8b2adf804c2691d075bb75fbb626be59f3769a2f4f1418554fc3de91a65451ab'
            'd7ab220204976b5802dbfa7cbd05ec0c5a9ff3524a2020b1b8deff4f601a05fa'
            '37344e6949b391a54491bcba68c2239393e442a00114ee36fe25d775d9656520'
            'd344df75ffbe9bd899e288862f41983c2e24b5eff3e7e8cf02d4891eeb137fff'
            '8fe103d8c0ae676d68ea518337024126bcdda7fc7befd57d5d2545e4f160736d'
            '8095031ee68edbb67323a562b54c9cf46aade7c91738950467b91eda68e434ee'
)



prepare() {
	cd ${srcdir}/fglrx-$pkgver
	/bin/sh ./amd-driver-installer-${pkgver}-x86.x86_64.run --extract archive_files
	#/bin/sh ./amd-catalyst-${pkgver}-linux-x86.x86_64.run --extract archive_files
	#/bin/sh ./amd-catalyst-${pkgver}-linux-x86.x86_64.run --extract archive_files
}

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  ## include ati_make.sh and use _ati_check function from it
	cd "${srcdir}"
	. ati_make.sh
	_ati_check

	if [ "${CARCH}" = "x86_64" ]; then
		BUILDARCH=x86_64
		_archdir=x86_64
	fi
	if [ "${CARCH}" = "i686" ]; then
		BUILDARCH=i386
		_archdir=x86
	fi

	cd "${srcdir}/fglrx-$pkgver/archive_files"

	## makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
	## works only in combination with ati_make.sh script
	patch -Np1 -i ../../makefile_compat.patch
	patch -Np1 -i ../../lano1106_fglrx_intel_iommu.patch
	patch -Np1 -i ../../lano1106_kcl_agp_13_4.patch
        patch -Np1 -i ../../cold-fglrx-3.14-current_euid.patch

	cd "${srcdir}/fglrx-$pkgver/archive_files/common/lib/modules/fglrx/build_mod"
        patch -Np1 -i ${srcdir}/kolasa_fglrx_312.patch
	cp "${srcdir}/fglrx-$pkgver/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" .
	cp 2.6.x/Makefile .

	## Compile module using _ati_check variables
	make -C /usr/lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
	MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
	PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules
}


package() {
	cd "${srcdir}/fglrx-$pkgver/archive_files/common/lib/modules/fglrx/build_mod"
	install -m755 -d "${pkgdir}/usr/lib/modules/${_extramodules}/"
	install -m644 fglrx.ko "${pkgdir}/usr/lib/modules/${_extramodules}/"
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

    ## License
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 "${srcdir}/fglrx-$pkgver/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}"

	## Install script
    sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install
    sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/" $startdir/*.install
}
