# Based on the file created for Arch Linux by:
# Maintainer : Piotr Gorski <sir_lucjan@bridgelinux.pl>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=xfce4-settings
pkgver=4.11.4
pkgrel=2
_git=a5f02e63df286f71625c776f394518d64ed18031
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'garcon' "libxfce4ui>=4.11" 'libnotify' 'libxklavier'
         'gnome-icon-theme' 'gtk-engines')
#makedepends=('intltool')
makedepends=('intltool' 'xfce4-dev-tools')
optdepends=('libcanberra: for sound control')
provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")
source=(#http://archive.xfce.org/src/xfce/$_pkgname/4.11/$_pkgname-$pkgver.tar.bz2
        $pkgname-$pkgver-$pkgrel.tar.bz2::http://git.xfce.org/xfce/$pkgname/snapshot/$pkgname-$_git.tar.bz2
        xfce4-settings-xml-4.11.2.patch
        hdmi-power-cycling.patch)
sha256sums=('f64292fc9b9adeaa780fa56736eae50334a7d4ab48406f8b98d014a7e71e11c1'
            'c912d6bb7fbfbd35b904a9f12f8fa2a18f4eb8ebf093392e691909cd494653ff'
            '4c8517e4bff5cb093996639e33214658ec372b50b856107188b9e701cb71068b')


prepare() {
  cd "$srcdir/$pkgname-$_git"
  #cd "$srcdir/$pkgname-$pkgver"

  # enable gnome icon theme, clearlooks theme and font hinting by default
  # (taken from Fedora)
  patch -Np1 -i "$srcdir/xfce4-settings-xml-4.11.2.patch"

  # Add hdmi power cycling patch
  # https://bugzilla.xfce.org/show_bug.cgi?id=11107
  patch -Np1 -i "$srcdir/hdmi-power-cycling.patch"

  sed -i -e 's|git]|]|' configure.ac.in
}

build() {
  cd "$srcdir/$pkgname-$_git"
  #cd "$srcdir/$pkgname-$pkgver"

  #./configure \
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-xrandr \
    --enable-xcursor \
    --disable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$_git"
  #cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
