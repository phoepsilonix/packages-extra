# Based on the file created for Arch Linux by: Frank Vanderham

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

# Contributor: Frank Vanderham <twelve.eighty (at) gmail.>
# Contributor: Hsin-Yi Chen (hychen) <hycehn (at) canonical.com>
# Contributor: Alberto Milone <alberto.milone (at) canonical.com>
# Contributor: Jasmine Hassan <jasmine.aura (at) gmail.com>
# Contributor: b3niup
# Upstream code branches: https://code.launchpad.net/ubuntu/+source/bcmwl
# https://bugs.launchpad.net/ubuntu/quantal/+source/bcmwl/+bug/923809 for first creation
# Also see: https://bbs.archlinux.org/viewtopic.php?id=145884

pkgname=linux312-broadcom-wl
_pkgname=broadcom-wl
pkgver=6.30.223.30
pkgrel=0.1
pkgdesc='Broadcom 802.11 Linux STA wireless driver BCM43142.'
url='https://bbs.archlinux.org/viewtopic.php?id=145884'
arch=('i686' 'x86_64')
license=('custom')
depends=('linux312')
makedepends=("broadcom-wl-dkms>=$pkgver"
             'dkms'
             'linux312' 'linux312-headers')
groups=('linux312-extramodules')
provides=("$_pkgname=$pkgver")
install=install
source=(broadcom-wl-dkms.conf)
sha256sums=(b97bc588420d1542f73279e71975ccb5d81d75e534e7b5717e01d6e6adf6a283)

build() {
  _extraver=extramodules-3.12-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf

  # build host modules
  msg2 'Build module'
  dkms --dkmsframework dkms.conf build "broadcom-wl/$pkgver" -k "$_kernver"
}

package_linux312-broadcom-wl(){
  _extraver=extramodules-3.12-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  depends=('linux312')

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/broadcom-wl/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
  install -D -m 644 "${srcdir}"/broadcom-wl-dkms.conf "${pkgdir}"/etc/modprobe.d/linux312-broadcom-wl.conf
}
