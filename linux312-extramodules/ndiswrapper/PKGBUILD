# Based on the file created for Arch Linux by:
# Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# SpepS <dreamspepser at yahoo dot it>
# Tobias Powalowski <tpowa@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

_pkgname=ndiswrapper
pkgbase=ndiswrapper
pkgname=('linux312-ndiswrapper' 'ndiswrapper-utils')
groups=('linux312-extramodules')
pkgver=1.61
pkgrel=1
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors."
license=('GPL')
arch=('i686' 'x86_64')
url="http://ndiswrapper.sourceforge.net"
depends=('linux312' 'wireless_tools' 'perl' "$_pkgname-utils")
makedepends=('linux312-headers')
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz")
options=('!strip')
sha256sums=('2ac1847c24cbfa4f48a800b04c9721219614f1663a4ac94af3c7939b45c47584')

build() {
  _extramodules=extramodules-3.12-MANJARO
  _kver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "$srcdir/$_pkgname-$pkgver"

  # modinfo path fix
  sed -i "/modinfo/s/s/usr\//" driver/Makefile

  # make sure we point to the right build directory
  sed -i "/^KBUILD/ s,.*,KBUILD = $(readlink -f /usr/lib/modules/$_kver/build)," driver/Makefile

  make KVERS=$_kver
}

package_linux312-ndiswrapper() {
  _extramodules=extramodules-3.12-MANJARO
  _kver="$(cat /usr/lib/modules/${_extramodules}/version)"
  provides=("$_pkgname=$pkgver")
  groups=('linux312-extramodules')
  install=ndiswrapper.install
  cd "$srcdir/$_pkgname-$pkgver"

  make INST_DIR="usr/lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" sbindir=/usr/bin usrsbindir=/usr/bin install

  # set the kernel we've built for inside the install script
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

  gzip "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

  # mv ndiswrapper-utils
  mkdir -p $srcdir/tmp-ndis-utils/usr
  mv -v "$pkgdir/usr/share" "$srcdir/tmp-ndis-utils/usr"
  mv -v "$pkgdir/usr/bin" "$srcdir/tmp-ndis-utils/usr"
}

package_ndiswrapper-utils() {
  pkgdesc="Utils for NDIS (Windows Network Drivers) drivers supplied by vendors."
  pkgrel=1
  cd $srcdir/tmp-ndis-utils
  mv -v usr "$pkgdir"
}

# vim:set ts=2 sw=2 et:
