# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>
# Contributor: Alex Filgueira <alexfilgueira@antergos.com>

_pkgname=thus
pkgname=thus
pkgver=0.8.13rc1
pkgrel=2
pkgdesc='Graphical Installer for Manjaro Linux'
arch=(any)
license=(GPL)
url="https://github.com/manjaro/thus.git"
depends=(coreutils cryptsetup ckbcomp gptfdisk grep libtimezonemap hwinfo lvm2 mhwd 
         pam_encfs python python-pyparted python-configobj python-gobject python-dbus 
         python-cairo python-yaml rsync sudo upower util-linux webkitgtk3 e2fsprogs 
         dosfstools jfsutils ntfs-3g ntfsprogs reiserfsprogs xfsprogs ttf-aboriginal-sans
         ttf-indic-otf ttf-khmer ttf-lohit-fonts ttf-myanmar3 ttf-thaana-fonts 
         ttf-tlwg iso-codes)
install=thus.install
backup=(etc/thus.conf)
_git=Yes
_development=No
_snapshot=f86699dd22e766a98af75039400642cadf68e288

if [ "$_git" == "Yes" ]; then
   makedepends=(git)
   source=(git+https://github.com/manjaro/$pkgname.git)
else
   if [ "${_snapshot}" != "" ]; then
       source=("$_pkgname-$pkgver-$pkgrel.tar.gz::$url/archive/$_snapshot.tar.gz")
   else
       source=("$_pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
   fi
fi

sha256sums=('SKIP')

prepare() {
	if [ "$_development" == "Yes" ]; then
		cd "$srcdir/$pkgname"
		git checkout development
	fi
}

package() {
	if [ -e "$srcdir/$_pkgname-$pkgver" ]; then
		cd "$srcdir/$_pkgname-$pkgver"
	fi
	if [ -e "$srcdir/$_pkgname" ]; then
		cd "$srcdir/$_pkgname"
	fi
	if [ -e "$srcdir/$_pkgname-$_snapshot" ]; then
		cd "$srcdir/$_pkgname-$_snapshot"
	fi

	install -d ${pkgdir}/usr/share/locale/
	install -d ${pkgdir}/usr/share/thus
	install -Dm755 "bin/thus" "${pkgdir}/usr/bin/thus"
	install -Dm755 "update.info" "$pkgdir/usr/share/thus/update.info"
	install -Dm644 "thus.desktop" ${pkgdir}/usr/share/applications/thus.desktop

	for i in thus data scripts ui; do
		cp -R ${i} "${pkgdir}/usr/share/thus/"
	done

	for i in etc; do
		cp -R ${i} "${pkgdir}/"
	done

	# copy en_GB as en
	cp po/en_GB.po po/en.po

  	for files in po/*.po; do
    	if [ -f "$files" ] && [ "$files" != 'po/thus.pot' ]; then
        	STRING_PO=`echo ${files#*/}`
        	STRING=`echo ${STRING_PO%.po}`
        	mkdir -p ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES
        	msgfmt $files -o ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES/thus.mo
        	echo "${STRING} installed..."
    	fi
	done
}

