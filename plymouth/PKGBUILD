# Based on the file created for Arch Linux by:
# Det <nimetonmaili at gmail dot com>
# PirateJonno <j@skurvy.no-ip.org>
# Biginoz <biginoz _at_ free _point_ fr>
# M0Rf30 <morfeo89@hotmail.it>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=plymouth
pkgver=0.8.8
pkgrel=9
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('libdrm' 'pango' 'systemd' 'gtk2')
makedepends=('docbook-xsl')
optdepends=('ttf-dejavu')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
backup=('etc/plymouth/plymouthd.conf')
source=("http://www.freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.bz2"
        'manjaro-logo.png'
        "plymouth.encrypt_hook"
        "plymouth.encrypt_install"
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'lightdm-plymouth.service'
        'lxdm-plymouth.service'
        'slim-plymouth.service'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-pid.patch'
        'plymouth-quit.service'
        'plymouth-quit.service.in.patch'
        'plymouth-quit-wait.service'
        'plymouth-set-default-theme.in.patch'
        'plymouth-update-initrd.patch'
        'plymouthd.conf'
        'systemd-unit-dir.patch'
        'udevadm.patch')
sha256sums=('1bada4e1d3a31a5a99adc2db83f5452d9818839cda51a6e430f044f6281d759b'
            '014e8a09f88a73b1e5985dcb16a44004e341f5bba90043fa3d7fd7e3a56120cf'
            'cbfb5bcb6897703e088eefdee9d578f75d987116becafecbb2018b283ba3865e'
            '544399d4f3454644f24ad11e504eaa7d622c73942728f52fb55bea054c756fa1'
            '53818ac8299edbb104e2fa09d376079c10a287da9f8a9b26bfb5fc8789b6bc3c'
            'f9528f0df99f0182c062cd505a49a1e475128e78a7fe535db2a64ee5103abfc8'
            '41d20d510eeec8aa1ee7a2cc6eb6134a2535bc47d2ade465b55dc06f1a3d9640'
            '86d0230d9393c9d83eb7bb430e6b0fb5e3f32e78fcd30f3ecd4e6f3c30b18f71'
            '6ffe016e8b03354b0a02e2e5d550a0f5f278fceccecfd8747619bdcf6ba9f800'
            '0cacaa68e164893ca97d06c7f06d56738a0902991585e456ce687bc2bc1705a0'
            'f9caa93e784ed1f045c6c12d39c0f89f5391ecfa8e12ba3f021a0a4d55234815'
            'c1d44564bad8c4d49ef7eb5083f0339f11ef2b3215ee8a5e6c56a586a8d31fac'
            'f95d11cc3222c10dfd0405ecbde6e82efa5e4580ab58e2372cce3bf04055aa25'
            'f576f97a3fa7a145201697cc9188d77e1917d4f1bb8e286270d1cd789adb22bb'
            '2fd66867c829e51308f460eda581aec4be42d5b1ae57a537baf6d594f9007f25'
            '59729e10edf2107a0d6b4dc1e77403b79ae14fe56a3964741a3ddf61714272e4'
            'cc33cd71264aab5acbe4919c337b9aa29ac5695c6b310a1992b1aab66dc4b95a'
            '1292cc3ac2b73020f2a94a0ec53b238ec7c7321b66e59d243d6b645f794499fb'
            'f965d311bd66e426723fae20161a152af59adc1831549d92bef874d868c50f31'
            '83ffd4aa52ca6a77423ab7edc2c2369808c7abffe68d97e134176c4592a81cc6'
            '703450baf386efe8d6c6a6b987672e98e746a5ca00e1457a655a7ae1164358e0'
            '85833e8545470c8a69b68b99364928d497efe014a2e767971f57e80d41b5f402')

prepare() {
    cd $srcdir 
    msg "Applying Patches..."
    
    cd $srcdir/$pkgname-$pkgver
    msg2 "Setting a human-readable error for '# plymouth-set-default-theme -R'"
    patch -p0 -i ../plymouth-set-default-theme.in.patch

    msg2 "Fixing updating initrd"
    patch -p0 -i ../plymouth-update-initrd.patch

    msg2 "Changing PID file to /run/plymouth.pid"
    patch -p1 -i ../plymouth-pid.patch

    msg2 "Changing plymout quit service"
    patch -p1 -i ../plymouth-quit.service.in.patch
    
    msg2 "Changing udevadm path"
    patch -p1 -i ../udevadm.patch  

    msg2 "Changing SYSTEMD_UNIT_DIR to /usr/lib/systemd/system/"
    patch -p0 -i ../systemd-unit-dir.patch
    
    sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c
}

build() {
    cd $srcdir/$pkgname-$pkgver
    
    msg "Starting make..."

    ./configure --prefix=/usr \
                --exec-prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libdir=/usr/lib \
                --libexecdir=/usr/lib \
                --sbindir=/usr/bin \
                --enable-systemd-integration \
                --enable-drm \
                --enable-tracing \
                --enable-pango \
                --without-rhgb-compat-link \
                --with-gdm-autostart-file=yes \
                --enable-gdm-transition \
                --with-gdm-autostart-file=yes \
                --with-release-file=/etc/manjaro-release \
                --with-logo=/usr/share/plymouth/manjaro-logo.png \
                --with-background-start-color-stop=0x000000 \
                --with-background-end-color-stop=0x4D4D4D \
                --without-system-root-install

    make
}

package() {
    cd $srcdir/$pkgname-$pkgver

    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
    install -Dm644 "$srcdir/plymouth.initcpio_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth"
    install -Dm644 "$srcdir/plymouth.encrypt_install" "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
    install -Dm644 "$srcdir/plymouth.encrypt_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
    install -Dm644 "$srcdir/plymouth.functions" "$pkgdir/etc/rc.d/functions.d/plymouth.functions"
    install -Dm644 "$srcdir/manjaro-logo.png" "$pkgdir/usr/share/plymouth/manjaro-logo.png"
    install -Dm644 "$srcdir/plymouthd.conf" "$pkgdir/etc/plymouth/plymouthd.conf"

    # Plymouth<->systemd integration, including units for DMs
    for i in {{gdm,kdm{,-unpatched},lightdm,lxdm,slim}-plymouth,plymouth-quit-wait,plymouth-quit}.service; do
        install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
    done
}
