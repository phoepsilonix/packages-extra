# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Based on SUSE spec https://build.opensuse.org/package/files?package=rt3562sta&project=driver%3Awireless

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgbase=rt3562sta
pkgname=('linux318-rt3562sta' 'rt3562sta-common')
pkgver=2.4.1.1_r1
_patchrel=${pkgver/*_r/}
pkgrel=3
_subpkgrel=3
arch=(i686 x86_64)
url="http://www.mediatek.com/en/Products/support.php?sn=501"
license=('GPL')
makedepends=('linux318' 'linux318-headers')
source=($pkgname-$pkgver.tar.gz::https://github.com/mtorromeo/rt3562sta-linux/archive/r${_patchrel}.tar.gz)

build() {
	_extramodules=extramodules-3.18-MANJARO
	KERNEL_RELEASE="$(cat /usr/lib/modules/$_extramodules/version)"

	cd "$srcdir/rt3562sta-linux-r$_patchrel"

	# clean up this mess of mixing RT2860STA with RT3562STA
	# in documentation files
	mv RT2860STA.dat RT3562STA.dat
	mv RT2860STACard.dat RT3562STACard.dat
	sed -i 's/2860/3562/g' *STA* iwpriv_usage.txt

	# as we change the default name of the interface from raX to wlanX, change respective references in documentation, too
	sed -i 's|ra0|wlan0|g' *.txt README* *.dat
	sed -i 's|ra1|wlan1|g' *.txt README* *.dat
	sed -i 's|ra2|wlan2|g' *.txt README* *.dat

	export EXTRA_CFLAGS="-DVERSION=$pkgver"

	# this Makefile is far too strict...
	echo "LINUX_SRC = /usr/lib/modules/$KERNEL_RELEASE/build" >> Makefile

	make
}

package_rt3562sta-common() {
	pkgdesc="Ralink RT3562 PCI WLAN adaptors common files"
        arch=(any)
        pkgrel="$_subpkgrel"
	cd "$srcdir/rt3562sta-linux-r$_patchrel"

	install -Dm 0640 RT3562STA.dat "$pkgdir/etc/Wireless/RT3562STA/RT3562STA.dat"
	install -dm 0755 "$pkgdir/usr/share/doc/$pkgbase"
	install -m 0644 iwpriv_usage.txt README* RT3562STA* sta_ate_iwpriv_usage.txt "$pkgdir/usr/share/doc/$pkgbase"
}

package_linux318-rt3562sta() {
	_extramodules=extramodules-3.18-MANJARO
	_kernver="$(cat /usr/lib/modules/$_extramodules/version)"
	provides=("$pkgbase=$pkgver")
	groups=('linux318-extramodules')
	pkgdesc="Ralink RT3562 PCI WLAN adaptors kernel module"
	depends=('linux318' 'rt3562sta-common')
	groups=('linux318-extramodules')
        install=$pkgbase.install
	cd "$srcdir/rt3562sta-linux-r$_patchrel"

	install -Dm 0644 os/linux/$pkgbase.ko "$pkgdir/usr/lib/modules/$_extramodules/$pkgbase.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${pkgbase}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

sha256sums=('6fd6306b6fb3886bbf9d284a28cc065a4cdfea18a03708e99aa4c7a038bcf02f')
