# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Based on SUSE spec https://build.opensuse.org/package/files?package=rt3562sta&project=driver%3Awireless

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgbase=rt3562sta
pkgname=('linux318-rt3562sta' 'rt3562sta-common')
pkgver=2.4.1.1_r4
_patchrel=${pkgver/*_r/}
pkgrel=20
_subpkgrel=20
arch=(i686 x86_64)
url="http://www.mediatek.com/en/Products/support.php?sn=501"
license=('GPL')
makedepends=('linux318' 'linux318-headers')
source=($pkgbase-$pkgver.tar.gz::https://github.com/mtorromeo/$pkgbase-linux/archive/r$_patchrel.tar.gz
        vfs_read_write.patch)
sha256sums=('6af6dd13ae1e1075974c8d0a54a0c9f73624cecf153f1f75c8c4f106f4719a1d'
            '00a5c178fb548f25dc0de6d810e85b96691695ee2d2fb3d22444b5180664fa8a')

prepare()  {
    # __vfs_read / __vfs_write where only introduced with linux319
    cd $srcdir/$pkgbase-linux-r$_patchrel
    patch -p1 -i $srcdir/vfs_read_write.patch
}

build() {
    _extramodules=extramodules-3.18-MANJARO
    KERNEL_RELEASE="$(cat /usr/lib/modules/$_extramodules/version)"

    cd "$srcdir"/rt3562sta-linux-r$_patchrel

    EXTRA_CFLAGS="-DVERSION=$pkgver" \
    LINUX_SRC="/usr/lib/modules/$KERNEL_RELEASE/build" \
    make
}

package_rt3562sta-common() {
    pkgdesc="Ralink RT3562 PCI WLAN adaptors common files"
    arch=(any)
    pkgrel="$_subpkgrel"
    cd "$srcdir/rt3562sta-linux-r$_patchrel"

    install -Dm 0640 RT3562STA.dat "$pkgdir/etc/Wireless/RT3562STA/RT3562STA.dat"
    install -dm 0755 "$pkgdir/usr/share/doc/$pkgbase"
    install -m 0644 iwpriv_usage.txt README* RT3562STA* sta_ate_iwpriv_usage.txt "$pkgdir/usr/share/doc/$pkgbase"
}

package_linux318-rt3562sta() {
    _extramodules=extramodules-3.18-MANJARO
    _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
    provides=("$pkgbase=$pkgver")
    groups=('linux318-extramodules')
    pkgdesc="Ralink RT3562 PCI WLAN adaptors kernel module"
    depends=('linux318' 'rt3562sta-common')
    groups=('linux318-extramodules')
    install=$pkgbase.install
    cd "$srcdir/rt3562sta-linux-r$_patchrel"

    install -Dm 0644 os/linux/$pkgbase.ko "$pkgdir/usr/lib/modules/$_extramodules/$pkgbase.ko"

    # set the kernel we've built for inside the install script
    sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${pkgbase}.install"

    find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
