# Maintainer: Philip MÃ¼ller <philm@manjarolinux.org>
# Co-Maintainer: Guinux <nuxgui@gmail.com>

pkgname=linux318-nvidiabl
_pkgname=nvidiabl
pkgver=0.88
pkgrel=16
epoch=
pkgdesc="Driver to adjust display backlight on modern mobile NVidia graphics adapters."
arch=('x86_64' 'i686')
url="https://github.com/guillaumezin/nvidiabl"
license=('GPL')
depends=()
makedepends=("linux318-headers")
optdepends=()
conflicts=(nvidia-bl)
provides=("$_pkgname=$pkgver")
groups=('linux318-extramodules')
options=()
install=nvidiabl.install
changelog=
_extramodules=extramodules-3.18-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

source=(https://github.com/guillaumezin/nvidiabl/archive/master.zip 
        nvidiabl-master.patch)
md5sums=('5fdda33fedcb78320c1581e84b395d39'
         '79e5699da970908d6ec8dba114df87cb')

prepare() {
  cd "${srcdir}/nvidiabl-master/"
  patch -p1 -i "${srcdir}/nvidiabl-master.patch"
}

build() {
  cd "${srcdir}/nvidiabl-master/"
  make KVER="${_kernver}"
}

package() {
  cd "${srcdir}/nvidiabl-master/"
  MODPATH="${pkgdir}/usr/lib/modules/${_extramodules}/"
  install -d $MODPATH
  install -m 644 -c nvidiabl.ko $MODPATH
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidiabl.install"
  sed -i -e "s/VERSION='.*'/VERSION='linux318'/" "${startdir}/nvidiabl.install"
}

# vim:set ts=2 sw=2 et:
