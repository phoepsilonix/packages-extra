# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

_linuxprefix=linux318
_extramodules=extramodules-3.18-MANJARO
pkgbase=$_linuxprefix-spl_zfs
pkgname=("$_linuxprefix-spl" "$_linuxprefix-zfs")
pkgver=0.7.8
_pkgver=spl-0.7.8
pkgrel=3
url="http://zfsonlinux.org/"
arch=('i686' 'x86_64')
license=("CDDL")
depends=("$_linuxprefix" "kmod")
makedepends=("$_linuxprefix-headers")
groups=("$_linuxprefix-extramodules")
install=install
source=("https://github.com/zfsonlinux/spl/archive/spl-${pkgver}.tar.gz"
        "https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz")
sha256sums=('4df2287853a2c1444ab020e68846dc446a2934f9c377fb14e9ed68481dc4bd64'
            '70ba0edd72914d4bfc9a9426cf26725e955a9509acbddb6902efb9eebb35f150')

prepare() {
  cd "${srcdir}/spl-${_pkgver}"
  # SPL patches here

  cd "${srcdir}/zfs-${pkgver}"
  # ZFS patches here
}

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "${srcdir}/spl-${_pkgver}"
  ./autogen.sh
  sed -i "s|\$(uname -r)|${_kernver}|g" configure
  ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
              --with-linux=/usr/lib/modules/${_kernver}/build \
              --with-config=kernel
  make
  cd "${srcdir}/zfs-${pkgver}"
  ./autogen.sh
  sed -i "s|\$(uname -r)|${_kernver}|g" configure
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --libdir=/usr/lib \
              --datadir=/usr/share --includedir=/usr/include --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-${pkgver} --with-config=kernel \
              --with-linux=/usr/lib/modules/${_kernver}/build \
              --with-spl=${srcdir}/spl-${_pkgver}
  make
}

package_linux318-spl(){
  _pkgname=spl
  pkgdesc='Solaris Porting Layer kernel modules.'
  provides=("$_pkgname=$pkgver")
  depends+=("spl-utils=${pkgver}")

  cd "${srcdir}/spl-${_pkgver}"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  install -m644 module/*/*.ko "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/install"
}

package_linux318-zfs(){
  _pkgname=zfs
  pkgdesc='Kernel modules for the Zettabyte File System.'
  provides=("$_pkgname=$pkgver")
  depends+=("$_linuxprefix-spl" "zfs-utils=${pkgver}")

  cd "${srcdir}/zfs-${pkgver}"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  install -m644 module/*/*.ko "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/install"
}
