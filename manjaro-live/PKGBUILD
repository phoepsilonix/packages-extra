# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>
# Contributor: anex <assassin.anex[@]gmail.com>

_repo=manjaro-tools-livecd

pkgbase=manjaro-live
pkgname=('manjaro-live-base'
	'manjaro-live-openrc'
	'manjaro-live-systemd'
	'manjaro-live-skel')
pkgver=20160504
pkgrel=1
pkgdesc='Manjaro Linux live session'
arch=('any')
url="https://github.com/manjaro/manjaro-tools-livecd"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
	date +%Y%m%d
}

build() {
	cd ${srcdir}/${_repo} #-${pkgver}
	make PREFIX=/usr
}

package_manjaro-live-base() {
	pkgdesc='Manjaro Linux live base scripts'
	depends=('manjaro-tools-base')
	conflicts=('manjaro-livecd-base')
	replaces=('manjaro-livecd-base')

	cd ${srcdir}/${_repo} #-${pkgver}
	make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-live-openrc() {
	pkgdesc='Manjaro Linux live openrc init scripts'
	depends=('openrc' 'manjaro-live-base')
	conflicts=('manjaro-livecd-openrc')
# 	replaces=('manjaro-livecd-openrc')

	cd ${srcdir}/${_repo} #-${pkgver}
	make PREFIX=/usr DESTDIR=${pkgdir} install_rc
}

package_manjaro-live-systemd() {
	pkgdesc='Manjaro Linux live systemd units'
	depends=('systemd' 'manjaro-live-base')
	conflicts=('manjaro-livecd-systemd')
# 	replaces=('manjaro-livecd-systemd')

	cd ${srcdir}/${_repo} #-${pkgver}
	make PREFIX=/usr DESTDIR=${pkgdir} install_sd
}

package_manjaro-live-skel() {
	pkgdesc='Manjaro Linux live session autostart items'

	cd ${srcdir}/${_repo} #-${pkgver}
	make PREFIX=/usr DESTDIR=${pkgdir} install_xdg
}
