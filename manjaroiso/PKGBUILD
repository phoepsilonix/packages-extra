#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgbase=manjaroiso
pkgname=('manjaroiso' 'manjaroiso-profiles') # 'manjaroiso-community-profiles')
pkgver=0.8.7.1
pkgrel=3
arch=('any')
url="http://git.manjaro.org/core/manjaroiso"
license=('GPL')
makedepends=('git')
_git=No

if [ "$_git" == "Yes" ]; then
   source=(git+http://git.manjaro.org/core/manjaroiso.git)
   sha256sums=('SKIP')
else
   source=("manjaroiso-$pkgver.tar.gz::http://git.manjaro.org/core/manjaroiso/repository/archive?ref=$pkgver"
           "http://git.manjaro.org/core/manjaroiso/raw/e4136268749abf8c1d83de1759324d077dbe07e4/src/buildiso")
   sha256sums=('663802052cf7b77be549e427ef8fe7780bb88ace7b359ece72a94001ff572bbc' 'SKIP')
fi

package_manjaroiso() {
  pkgdesc="Manjaro Linux livecd/liveusb generation scripts"
  depends=('devtools' 'mkinitcpio' 'mkinitcpio-nfs-utils' 'nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'haveged' 'arch-install-scripts' 'pacman>=4.1')
  optdepends=('qemu: quickly test isos' 'manjaroiso-profiles: Manjaro Linux ISO-Image profiles')
  cd ${srcdir}/${pkgbase}/src
  make DESTDIR="$pkgdir" install
  cp -v ${srcdir}/buildiso $pkgdir/usr/bin/buildiso
  cd ${srcdir}/${pkgbase}/
  mkdir -p $pkgdir/usr/share/manjaroiso/configs
  cp -r configs/shared $pkgdir/usr/share/manjaroiso/configs/shared
  sed -i "s|VERSION|${pkgver}-${pkgrel}|g" $pkgdir/usr/share/manjaroiso/functions/messages
}

package_manjaroiso-profiles() {
  pkgdesc="Manjaroiso Profiles"
  depends=('manjaroiso')
  cd ${srcdir}/${pkgbase}/
  mkdir -p $pkgdir/usr/share/manjaroiso/configs
  for conf in build net openbox xfce
  do
      cp -r configs/${conf} $pkgdir/usr/share/manjaroiso/configs/${conf}
  done
}

package_manjaroiso-community-profiles() {
  pkgdesc="Manjaroiso Community Profiles"
  depends=('manjaroiso')
  cd ${srcdir}/${pkgbase}/
  mkdir -p $pkgdir/usr/share/manjaroiso/configs
  for conf in cinnamon e17 kde lxde mate
  do
      cp -r configs/${conf} $pkgdir/usr/share/manjaroiso/configs/${conf}
  done
}
